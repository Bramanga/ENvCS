;ok some important variables
SET [0x5ffA], 0 ;did we print a command?
SET [0x7FFF], 0x8000 ;video pointer
SET [0x5FFB], 0x0000 ;current menu item
SET PC, boot

:unknownc    DAT " Unknown Command", 0
:intro       DAT " Loading Env v 0.3.0", 0
:hardware    DAT " Hardware probing and initialization will go here", 0
:typehelp    DAT " Type 'help' for a list of commands", 0
:helpout     DAT " Current commands are: help, reset, about, load, gui", 0
:abouttext   DAT " ENvCS, Written by Hjax, v0.3.0, type 'help' to view a list of commands", 0
:welcome     DAT " Welcome to ENvCS", 0
:prompt      DAT "> ", 0

;menu items for the gui
:auto        DAT "AUTO", 0
:shield      DAT "SHIELD", 0
:drive       DAT "DRIVE", 0
:weapon      DAT "WEAPON", 0
:ftl         DAT "FTL", 0
:doublespace DAT "  ", 0

:boot        SET B, intro
             JSR printnl
             SET B, hardware
             JSR printnl
             JSR loadbarinit
             ADD [0x7FFF], 1
             SET C, 0x001E
:bootload    JSR loadbarADD
             SUB C, 0x0001
             IFN C, 0x0000
             SET PC, bootload
             JSR newline
             SET B, welcome
             JSR printnl
             SET B, typehelp
             JSR printnl
             SET B, prompt
             JSR printdat
             SET PC, main

:printdat    IFE [0x7FFf], 0x8200
             JSR scroll
	     SET A, [B]
             IFE A, 0
             SET PC, POP
	     BOR A, 0xf100
	     SET C, [0x7FFF]
             SET [C], A
	     ADD B, 1
	     ADD [0x7FFF], 1
	     SET PC, printdat

:printnl     JSR printdat
             JSR newline
             SET PC, POP

:append      BOR I, 0xf100
             SET C, [0x7FFF]
             SET [C], I
             ADD [0x7FFF], 1
             SET PC, POP

:reset       SET J, 0x0000
             SET [0x7FFC], 0x0000
             JSR clearScreen
             SET [0x7FFF], 0x8000
             SET [0x5FFA], 1
             SET B, prompt
             JSR printdat
             SET PC, POP

:main        IFE [0x9000], 0     ; if the user hasn't typed anything in yet
             SET PC, main        ;continue waiting
             IFE [0x9000], 0xA   ;the user hit enter, proccess the command!
             JSR command
             IFE [0x9000], 0x8   ;the user hit the delete key
             SET PC, delete
             SET I, [0x9000]
             SET [0x9000], 0
             SET B, [0x7FFF]
             MOD B, 32
             IFE B, 0
             SET PC, main
             IFN I, 0
             jsr append
             SET PC, main

:delete      SUB [0x7FFF], 1
             SET Z, [0x7FFF]
             SET J, Z
             MOD J, 32
             IFE J, 1
             ADD [0x7FFF], 1
             SET [Z], 0x0000
             SET [0x9000], 0
             SET PC, main

:newline
  	     SET i, [0x7FFF]
 	     MOD i, 32
	     SET z, 32
 	     SUB z, i
 	     ADD [0x7FFF], z
 	     SET pc, POP

:command     SET [0x9000], 0
             SET [0x5FFA], 0
             JSR newline
             SET Y, [0x7FFF]
             SUB Y, 0x001E
             IFE [Y], 0xF168
             JSR help
             IFE [y], 0xF172
             JSR reset
             IFE [y], 0xF161
             JSR about
             IFE [y], 0xF16C
             JSR loadbardemo
             IFE [y], 0xF167
             JSR gui
             IFE [0x5FFA], 0
             JSR unknown
             SET Z, 0x0000
             SET Y, 0x0000
             SET PC, POP

:unknown     SET B, unknownc
             JSR printdat
             JSR newline
             SET B, prompt
             JSR printdat
             SET PC, POP
:about       SET B, abouttext
             JSR printdat
             JSR newline
             SET B, prompt
             JSR printdat
             SET [0x5FFA], 1
             SET pc, POP
:help        SET b, helpout
             JSR printdat
             SET [0x5FFA], 1
             JSR newline
             SET b, prompt
             JSR printdat
             SET PC, POP
:loadbarinit SET C, [0x7FFF]
             SET X, 0x003C
             BOR X, 0xf400
             SET Y, 0x003E
             BOR Y, 0xf400
             SET [C], X
             ADD C, 0x001F
             SET [C], Y
             SET [0x5FFA], 1
             SET PC, POP

:loadbardemo JSR loadbarinit
             ADD [0x7FFF], 1
             JSR loadbarkb
             SET [0x5FFA], 1
             JSR newline
             SET b, prompt
             JSR printdat
             SET PC, POP

:loadbarkb   IFe [0x9000], 0x0000
             SET PC, loadbarkb
             SET Z, [0x9000]
             BOR z, 0xf100
             IFE Z, 0xF102
             JSR loadbarADD
             IFE Z, 0xF101
             JSR loadbardel
             IFE Z, 0xF171
             SET [0x9000], 0
             IFE Z, 0xF171
             SET Pc, POP
             SET [0x9000], 0
             SET PC, loadbarkb

:loadbarADD  SET I, 0x0020
             SET Y, [0x7FFF]
             SUB Y, 0x8000
             MOD Y, 32
             IFE J, 1
             ADD [0x7FFF], 1
             IFE Y, 31
             SUB [0x7FFF], 1
             BOR I, 0xf200
             SET a, [0x7FFF]
             SET [a], I
             ADD [0x7FFF], 1
             SET J, 0
             SET PC, POP

:loadbardel  IFE J, 0
             SUB [0x7FFF], 1
             SET Y, [0x7FFF]
             SUB Y, 0x8000
             MOD y, 32
             IFE Y, 0
             ADD [0x7FFF], 1
             IFE y, 31
             SUB [0x7FFF], 1
             SET a, [0x7FFF]
             SET [a], 0x0000
             SET [0x9000], 0
             SUB [0x7FFF], 1
             SET J, 1
             SET PC, POP
:scroll
 	     SET x, 0x8000
             SET z, 0x8020
             SET j, sp			; nifty stack pointer abuse :P
             SET sp, 0x8000

:scroll_loop
	     SET POP, [z]
	     SET POP, [1+z]
	     SET POP, [2+z]
	     SET POP, [3+z]
	     SET POP, [4+z]
	     SET POP, [5+z]
	     SET POP, [6+z]
	     SET POP, [7+z]
	     SET POP, [8+z]
	     SET POP, [9+z]
	     SET POP, [10+z]
	     SET POP, [11+z]
	     SET POP, [12+z]
	     SET POP, [13+z]
	     SET POP, [14+z]
	     SET POP, [15+z]
	     SET POP, [16+z]
	     SET POP, [17+z]
	     SET POP, [18+z]
	     SET POP, [19+z]
	     SET POP, [20+z]
	     SET POP, [21+z]
	     SET POP, [22+z]
	     SET POP, [23+z]
	     SET POP, [24+z]
	     SET POP, [25+z]
	     SET POP, [26+z]
	     SET POP, [27+z]
	     SET POP, [28+z]
	     SET POP, [29+z]
	     SET POP, [30+z]
	     SET POP, [31+z] ; unrolled loop. So sue me :P
	     ADD z, 32
	     IFG z, 0x8231
	     SET pc, scroll_end
	     SET pc, scroll_loop

:scroll_end
             SET sp, j
	     SET [0x1335], 0x81e0
             SUB [0x7FFF], 0x0020
	     SET pc, POP

:clearScreen
             IFN [0x7FFC], 0x0000
             BOR J, [0x7FFC]
             SET i, sp
             SET sp, [0x7FFF]
:clearScreen_loop
             SET PUSH, J
	     SET PUSH, J
	     SET PUSH, J
	     SET PUSH, J
	     SET PUSH, J
	     SET PUSH, J
	     SET PUSH, J
	     SET PUSH, J
	     SET PUSH, J
	     SET PUSH, J
	     SET PUSH, J
	     SET PUSH, J
	     SET PUSH, J
	     SET PUSH, J
	     SET PUSH, J
	     SET PUSH, J
	     SET PUSH, J
	     SET PUSH, J
	     SET PUSH, J
	     SET PUSH, J
	     SET PUSH, J
	     SET PUSH, J
	     SET PUSH, J
	     SET PUSH, J
	     SET PUSH, J
	     SET PUSH, J
	     SET PUSH, J
	     SET PUSH, J
	     SET PUSH, J
	     SET PUSH, J
	     SET PUSH, J
	     SET PUSH, J
	     IFG 0x8000, sp
	     SET pc, clearScreen_done
	     SET pc, clearScreen_loop

:clearScreen_done
             SET sp, i
	     SET pc, POP

:gui         SET J, 0x0000
             SET [0x7FFC], 0xF600
             SET [0x7FFF], 0x8200
             JSR clearScreen
             SET [0x7FFF], 0x8000
             SET [0x5FFA], 1
             SET [0x5FFB], 1
             JSR draw
             JSR selectmenu
             JSR drawloop
             SET PC, POP

:draw        SET [0x7FFF], 0x8000
             SET B, auto
             JSR drawmenu
             SET B, doublespace
             JSR drawmenu
             SET B, shield
             JSR drawmenu
             SET B, doublespace
             JSR drawmenu
             SET B, drive
             JSR drawmenu
             SET B, doublespace
             JSR drawmenu
             SET B, weapon
             JSR drawmenu
             SET B, doublespace
             JSR drawmenu
             SET B, ftl
             JSR drawmenu
             SET PC, POP

:drawmenu    SET A, [B]
             IFE A, 0x0000
             SET PC, POP
	     BOR A, 0xf100
	     SET C, [0x7FFF]
             SET [C], A
	     ADD B, 1
	     ADD [0x7FFF], 1
	     SET PC, drawmenu

:drawloop    IFE [0x9000], 0x0000
             SET PC, drawloop
             SET Z, [0x9000]
             BOR Z, 0xF100
             IFE Z, 0xF102
             ADD [0x5FFB], 1
             IFE Z, 0xF101
             SUB [0x5FFB], 1
             IFG [0x5FFB], 5
             SET [0x5FFB], 5
             IFG 1, [0x5FFB]
             SET [0x5FFB], 1
             SET [0x9000], 0x0000
             JSR menumove
             IFE Z, 0xF171
             SET Pc, resetterm
             SET PC, drawloop

:resetterm   SET [0x7FFF], 0x8200
             JSR reset
             SET PC, POP

:menumove    JSR draw
             JSR selectmenu
             SET PC, POP

:selectmenu  IFE [0x5FFB], 1
             SET B, auto
             IFE [0x5FFB], 2
             SET B, shield
             IFE [0x5FFB], 3
             SET B, drive
             IFE [0x5FFB], 4
             SET B, weapon
             IFE [0x5FFB], 5
             SET B, ftl
             IFE [0x5FFB], 1
             SET C, 0x8000
             IFE [0x5FFB], 2
             SET C, 0x8006
             IFE [0x5FFB], 3
             SET C, 0x800E
             IFE [0x5FFB], 4
             SET C, 0x8015
             IFE [0x5FFB], 5
             SET C, 0x801D
:selectloop  SET A, [B]
             IFE A, 0x0000
             SET PC, POP
             BOR A, 0xF200
             SET [C], A
             ADD B, 1
             ADD C, 1
             SET PC, selectloop


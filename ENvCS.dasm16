;ok some important variables
SET [0x5FFA], 0 ;did we print a command?
SET [0x5FFD], 0x8000 ;video pointer
SET [0x5FFB], 0x0000 ;current menu item
SET [0x5FFC], 0x9000 ;keyboard pointer
SET [0x5FFF], 0x0000 ;gui memory
SET [0x5FF0], 0x0000 ; first toggle
SET PC, boot
:loadbarend
:space       DAT " ", 0
:keypointer  dat 0
:unknownc    DAT " Unknown Command", 0
:intro       DAT " Loading Env v 0.3.0", 0
:hardware    DAT " Hardware probing and initialization will go here", 0
:typehelp    DAT " Type 'help' for a list of commands", 0
:helpout     DAT " Current commands are: help, reset, about, load, gui", 0
:abouttext   DAT " ENvCS, Written by Hjax, v0.3.0, type 'help' to view a list of commands", 0
:welcome     DAT " Welcome to ENvCS", 0
:prompt      DAT "> ", 0

;menu items for the gui
:auto        DAT "AUTO", 0
:shield      DAT "SHIELD", 0
:drive       DAT "DRIVE", 0
:weapon      DAT "WEAPON", 0
:ftl         DAT "FTL", 0
:doublespace DAT "  ", 0


:kbinput     set Z,[keypointer]
	     add Z, 0x9000
             SET A, Z
	     set Z,[Z]
	     set [A], 0
	     add [keypointer], 1
	     and [keypointer], 0xf
             SET PC, POP

:boot        SET B, intro
             JSR printnl
             SET B, hardware
             JSR printnl
             JSR loadbarinit
             ADD [0x5FFD], 1
             SET C, 0x001E
:bootload    JSR loadbarADD
             SUB C, 0x0001
             IFN C, 0x0000
             SET PC, bootload
             JSR newline
             SET B, welcome
             JSR printnl
             SET B, typehelp
             JSR printnl
             SET B, prompt
             JSR printdat
             SET PC, main

:printdat    IFG [0x5FFD], 0x817F
             JSR scrollstart
	     SET A, [B]
             IFE A, 0
             SET PC, POP
	     BOR A, 0xf000
	     SET C, [0x5FFD]
             ADD C, 1
	     ADD B, 1
             SET [C], A
	     ADD [0x5FFD], 1
	     SET PC, printdat

:printnl     JSR printdat
             JSR newline
             SET PC, POP

:append      BOR I, 0xf000
             SET C, [0x5FFD]
             SET [C], I
             ADD [0x5FFD], 1
             SET PC, POP

:reset       SET J, 0x0000
             SET [0x7FFC], 0x0000
             JSR clearScreen
             SET [0x5FFD], 0x8000
             SET [0x5FFA], 1
             SET B, prompt
             JSR printdat
             SET PC, POP

:main        JSR kbinput
             IFE Z, 0x0000     ; if the user hasn't typed anything in yet
             SET PC, main        ;continue waiting
             IFE Z, 0xA   ;the user hit enter, proccess the command!
             SET PC, command
             IFE Z, 0x8   ;the user hit the delete key
             SET PC, delete
             SET I, Z
             SET Z, 0
             SET B, [0x5FFD]
             MOD B, 32
             IFE B, 0
             SET PC, main
             IFN I, 0
             jsr append
             SET PC, main

:delete      SUB [0x5FFD], 1
             SET Z, [0x5FFD]
             SET J, Z
             MOD J, 32
             IFG 2, J
             ADD Z, 1
             IFG 2, J
             ADD [0x5FFD], 1
             SET [Z], 0x0000
             SET [0x9000], 0
             SET PC, main

:newline
  	     SET i, [0x5FFD]
 	     MOD i, 32
	     SET z, 32
 	     SUB z, i
 	     ADD [0x5FFD], z
 	     SET pc, POP

:command     SET [0x9000], 0
             SET [0x5FFA], 0
             JSR newline
             SET Y, [0x5FFD]
             SUB Y, 0x001E
             IFE [Y], 0xF068
             JSR help
             IFE [y], 0xF072
             JSR reset
             IFE [y], 0xF061
             JSR about
             IFE [y], 0xF06C
             JSR loadbardemo
             IFE [y], 0xF067
             JSR gui
             IFE [0x5FFA], 0
             JSR unknown
             SET Z, 0x0000
             SET Y, 0x0000
             SET PC, main

:unknown     SET B, unknownc
             JSR printdat
             JSR newline
             SET B, prompt
             JSR printdat
             SET PC, POP
:about       SET B, abouttext
             JSR printdat
             JSR newline
             SET B, prompt
             JSR printdat
             SET [0x5FFA], 1
             SET pc, POP
:help        SET b, helpout
             JSR printdat
             SET [0x5FFA], 1
             JSR newline
             SET b, prompt
             JSR printdat
             SET PC, POP
:loadbarinit SET C, [0x5FFD]
             SET X, 0x003C
             BOR X, 0xf400
             SET Y, 0x003E
             BOR Y, 0xf400
             SET [C], X
             ADD C, 0x001F
             SET [C], Y
             SET [0x5FFA], 1
             SET PC, POP

:loadbardemo JSR loadbarinit
             ADD [0x5FFD], 1
             JSR loadbarkb
             SET [0x5FFA], 1
             JSR newline
             SET b, prompt
             JSR printdat
             SET PC, POP

:loadbarkb   JSR kbinput
             IFE Z, 0x0000
             SET PC, loadbarkb
             BOR z, 0xf100
             IFE Z, 0xF12C
             JSR loadbardel
             IFE Z, 0xF12E
             JSR loadbaradd
             IFE Z, 0xF171
             SET Pc, POP
             SET PC, loadbarkb

:loadbaradd  SET I, 0x0020
             SET Y, [0x5FFD]
             SUB Y, 0x8000
             MOD Y, 32
             IFE J, 1
             ADD [0x5FFD], 1
             IFE Y, 31
             SUB [0x5FFD], 1
             BOR I, 0xf200
             SET a, [0x5FFD]
             SET [a], I
             ADD [0x5FFD], 1
             SET J, 0
             SET PC, POP

:loadbardel  IFE J, 0
             SUB [0x5FFD], 1
             SET Y, [0x5FFD]
             SUB Y, 0x8000
             MOD y, 32
             IFE Y, 0
             ADD [0x5FFD], 1
             IFE y, 31
             SUB [0x5FFD], 1
             SET a, [0x5FFD]
             SET [a], 0x0000
             SET [0x9000], 0
             SUB [0x5FFD], 1
             SET J, 1
             SET PC, POP

:wipe        SET Z, 0x8160
:wipetwo     SET [Z], 0x0000
             IFG Z, 0x8180
             SET PC, POP
             ADD Z, 1
             SET PC, wipetwo

:scrollstart SET A, 0x8000
:slowscroll  IFG A, 0x8180
             SET PC, donescroll
             SET C, A
             SUB C, 0x0020
             SET [C], [A]
             IFE 0x8160, A
             SET [A], 0x0000
             ADD A, 1
             SET PC, slowscroll
:donescroll  SET [0x5FFD], 0x8160
             JSR wipe
             SET PC, POP

:scroll
 	     SET x, 0x8000
             SET C, 0x8020
             SET j, sp			; nifty stack pointer abuse :P
             SET sp, 0x8000

:scroll_loop
	     SET POP, [c]
	     ADD C, 1
	     IFG C, 0x8211
	     SET pc, scroll_end
	     SET pc, scroll_loop

:scroll_end
             SET sp, j
	     SET [0x1335], 0x81e0
             SUB [0x5FFD], 0x0020
	     SET pc, POP

:clearScreen
             SET i, sp
             SET sp, [0x5FFD]
:clearScreen_loop
             SET PUSH, J
	     IFG 0x8000, sp
	     SET pc, clearScreen_done
	     SET pc, clearScreen_loop

:clearScreen_done
             SET sp, i
	     SET pc, POP

:gui         SET J, space
             SET [0x7FFC], 0xF600
             SET [0x5FFD], 0x8200
             JSR clearScreen
             SET [0x5FFD], 0x8000
             SET [0x5FFA], 1
             SET [0x5FFB], 1
             JSR draw
             JSR selectmenu
             SET [0x5FFD], 0x8040
             JSR loadbarinit
             ADD [0x5FFD], 1
             JSR drawloop
             SET PC, POP

:regui       SET J, 0x0000
             SET [0x7FFC], 0xF600
             SET [0x5FFD], 0x8200
             JSR clearScreen
             SET [0x5FFD], 0x8000
             SET [0x5FFA], 1
             JSR draw
             JSR selectmenu
             SET [0x5FFD], 0x8040
             IFE [0x5FFB], 1
             JSR loadbarinit
             ADD [0x5FFD], 1
             SET PC, POP

:draw        SET [0x5FFD], 0x8000
             SET B, auto
             JSR drawmenu
             SET B, doublespace
             JSR drawmenu
             SET B, shield
             JSR drawmenu
             SET B, doublespace
             JSR drawmenu
             SET B, drive
             JSR drawmenu
             SET B, doublespace
             JSR drawmenu
             SET B, weapon
             JSR drawmenu
             SET B, doublespace
             JSR drawmenu
             SET B, ftl
             JSR drawmenu
             SET PC, POP

:drawmenu    SET A, [B]
             IFE A, 0x0000
             SET PC, POP
	     BOR A, 0xf100
	     SET C, [0x5FFD]
             SET [C], A
	     ADD B, 1
	     ADD [0x5FFD], 1
	     SET PC, drawmenu

:drawloop    JSR kbinput
             IFE Z, 0x0000
             SET PC, drawloop
             SET [0x5FFF], [0x5FFB]
             BOR Z, 0xF100
             IFE Z, 0xF16C
             ADD [0x5FFB], 1
             IFE Z, 0xF16B
             SUB [0x5FFB], 1
             IFG [0x5FFB], 5
             SET [0x5FFB], 5
             IFG 1, [0x5FFB]
             SET [0x5FFB], 1
             SET [0x9000], 0x0000
             IFE Z, 0xF16C
             JSR menumove
             IFE Z, 0xF16B
             JSR menumove
             IFE Z, 0xF12C
             JSR loadbardel
             IFE Z, 0xF12E
             JSR loadbaradd
             IFE Z, 0xF171
             SET Pc, resetterm
             SET PC, drawloop

:resetterm   SET [0x5FFD], 0x8200
             JSR reset
             SET PC, POP

:menumove    JSR backup
             JSR regui
             JSR selectmenu
             IFE [0x5FFB], 1
             JSR restore
             SET PC, POP

:backup      IFN [0x5FFF], 1
             ADD PC, 1
             SET [0x5FFE], [0x5FFD]
             SET PC, POP

:restore     SET X, [0x5FFE]
             SUB X, 0x8041
:restoretwo  IFG 1, X
             SET PC, POP
             JSR loadbaradd
             SUB X, 1
             SET PC, restoretwo

:selectmenu  IFE [0x5FFB], 1
             SET B, auto
             IFE [0x5FFB], 2
             SET B, shield
             IFE [0x5FFB], 3
             SET B, drive
             IFE [0x5FFB], 4
             SET B, weapon
             IFE [0x5FFB], 5
             SET B, ftl
             IFE [0x5FFB], 1
             SET C, 0x8000
             IFE [0x5FFB], 2
             SET C, 0x8006
             IFE [0x5FFB], 3
             SET C, 0x800E
             IFE [0x5FFB], 4
             SET C, 0x8015
             IFE [0x5FFB], 5
             SET C, 0x801D
:selectloop  SET A, [B]
             IFE A, 0x0000
             SET PC, POP
             BOR A, 0xF200
             SET [C], A
             ADD B, 1
             ADD C, 1
             SET PC, selectloop

:toggleon    SET C, [0x5FFD]
             SET A, [0x5FFD]
             ADD C, 4
             IFE A,  [C]
             ;SET PC, toggleontwo
             SET B, 0x0020
             BOR B, 0xF100
             SET [A], B
             ADD [0x5FFD], 1
             SET PC, toggleon
